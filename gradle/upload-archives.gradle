apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.userfeeds.sdk'
version = '0.0.7'

if (System.env.CI != "true") {

    signing {
        sign configurations.archives
    }
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
    }
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
    artifacts {
        archives javadocJar
        archives sourcesJar
    }
    uploadArchives.repositories.mavenDeployer {

        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: userfeedsSonatypeUserName, password: userfeedsSonatypePassword)
        }
        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: userfeedsSonatypeUserName, password: userfeedsSonatypePassword)
        }
        pom.project {
            name 'Userfeeds Android SDK'
            description 'Userfeeds Android SDK'
            inceptionYear '2017'
            url 'https://github.com/Userfeeds/android-sdk'
            packaging 'aar'

            developers {
                developer {
                    name 'Maciej GÃ³rski'
                    id 'mg6maciej'
                    email 'maciek.gorski@gmail.com'
                    url 'https://github.com/mg6maciej'
                }
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/Userfeeds/android-sdk'
                connection 'scm:git:git@github.com:Userfeeds/android-sdk.git'
                developerConnection 'scm:git:git@github.com:Userfeeds/android-sdk.git'
            }
        }
    }
}
